	/ B RUNTIME
	*0
	0		/ INTERRUPT HANDLER
	HLT

	*20		/ RUNTIME FUNCTIONS
ENTER=	JMS I .
	XENTER

LEAVE=	JMS I .
	XLEAVE

MUL=	JMS I .
	XMUL

DIV=	JMS I .
	XDIV

MOD=	JMS I .
	XMOD

TMP1,	0		/ RUNTIME SCRATCH REGISTERS
TMP2,	0
TMP3,	0

BREG=	30		/ B RUNTIME REGISTERS

	*200		/ ENTRY POINT
ENTRY,	KCC		/ CLEAR STRAY INPUT
	TPC		/ SET TRANSMITTED FLAG
	JMS I XMAIN
	DCA TMP1	/ EXIT STATUS
	JMS EXIT
	TMP1
XMAIN,	MAIN

			/ RUNTIME SUPPORT

XBREG,	BREG-1
CR,	15
NEGCR,	-15
NL,	12
NEGNL,	-12
GETMSK,	177

XENTER,	0		/ FUNCTION PROLOGUE
	STA		/ LOAD -1
	TAD I XENTER	/ POINTER TO BEFORE FRAME AREA
	DCA 0010	/ DEPOSIT TO INDEX REGISTER
	TAD I 0010	/ NEG. NUMBER OF REGISTERS TO SAVE
	SNA		/ IF NOTHING TO SAVE
	 JMP SAVEND	/ SKIP SAVING
	DCA TMP1	/ REMEMBER AS LOOP COUNTER
	TAD XBREG	/ BEFORE FIRST B REGISTER
	DCA 0011	/ DEPOSIT TO INDEX REGISTER
SAVBEG,	TAD I 0011	/ LOAD REGISTER
	DCA I 0010	/ SAVE REGISTER
	ISZ TMP1	/ DONE SAVING?
	 JMP SAVBEG	/ CONTINUE
SAVEND,	TAD I 0010	/ NEG. NUMBER OF ARGUMENTS
	SNA		/ IF NO ARGUMENTS
	 JMP ARGEND	/ SKIP SETTING UP PARAMETERS
	DCA TMP1	/ SET UP LOOP COUNTER
	STA CLL RAL	/ LOAD -2
	TAD XENTER	/ POINTER TO CALLER'S RETURN ADDRESS
	DCA TMP2	/ REMEMBER
	STA		/ LOAD -1
	TAD I TMP2	/ BEFORE CALLERS RETURN ADDRESS
	DCA 0011	/ DEPOSIT TO INDEX REGISTER
ARGBEG,	TAD I 0011	/ POINTER TO ARGUMENT
	DCA TMP3	/ REMEMBER
	TAD I TMP3	/ ARGUMENT
	DCA I 0010	/ DEPOSIT TO ARGUMENT AREA
	ISZ TMP1	/ DONE SETTING UP PARAMETERS?
	 JMP ARGBEG	/ CONTINUE
	CLA IAC		/ LOAD 1
	TAD 0011	/ INSTRUCTION AFTER FUNCTION ARGUMENTS
	DCA I TMP2	/ MAKE CALLER RETURN TO AFTER THEM
ARGEND,	JMS RESTOR	/ SET UP FRAME TEMPLATE
TMPEND,	ISZ XENTER	/ SKIP OVER ARGUMENT
	JMP I XENTER	/ RETURN

RESTOR,	0		/ COPY -TMP1 WORDS FROM I 0010
	TAD I 0010	/ LOAD COUNT
	SNA		/ IF NOTHING TO COPY
	 JMP I RESTOR	/  RETURN
	DCA TMP1	/ SET UP COUNTER
	TAD XBREG	/ BEFORE FIRST B REGISTER
	DCA 0011	/ SET UP DESTINATION
RESTL,	TAD I 0010	/ LOAD REGISTER
	DCA I 0011	/ RESTORE REGISTER
	ISZ TMP1	/ DONE RESTORING?
	 JMP RESTL	/  CONTINUE
	JMP I RESTOR	/ RETURN

XLEAVE,	0		/ FUNCTION EPILOGUE
	DCA TMP3	/ REMEMBER RETURN VALUE
	TAD I XLEAVE	/ POINTER TO CALLER
	DCA XLEAVE	/ REMOVE LEVEL OF INDIRECTION
	TAD I XLEAVE	/ RETURN ADDRESS
	DCA TMP2	/ REMEMBER
	ISZ XLEAVE	/ POINTER TO ENTER INSTRUCTION
	ISZ XLEAVE	/ POINTER TO POINTER TO FRAME AREA
	STA		/ LOAD -1
	TAD I XLEAVE	/ POINTER TO BEFORE FRAME ARA
	DCA 0010	/ SET UP INDEX REGISTER
	JMS RESTOR	/ RESTORE REGISTERS
	TAD TMP3	/ RETURN VALUE
	JMP I TMP2	/ RETURN FROM CALLER

			/ STANDARD LIBRARY

EXIT,	0		/ EXIT PROGRAM
	CLA
	TAD I EXIT	/ LOAD EXIT STATUS FOR DISPLAY
	HLT		/ HALT
	JMP ENTRY	/ RESTART WHEN REQUESTED BY OPERATOR

GETCHA,	0		/ GET CHARACTER
	KSF		/ WAIT FOR INPUT
	 JMP .-1
	KRB		/ RECEIVE CHARACTER
	AND GETMSK	/ CLEAR HIGH BIT
	DCA TMP2	/ DEPOSIT A COPY
	TAD NEGCR	/ LOAD -CR
	TAD TMP2	/ TMP == CR?
	SZA CLA		/ IF WE GOT A CR
	 JMP NOTCR
	TAD NL		/ LOAD NL
	DCA TMP2	/ TRANSLATE CR TO NL
NOTCR,	JMS PUTCHA	/ ECHO CHARACTER
	TMP2		/ OUR IMPL. OF PUTCHAR RETURNS ITS ARGUMENT
	JMP I GETCHA	/ RETURN

PUTCHA,	0		/ PUT CHARACTER
	CLA
	TAD I PUTCHA	/ LOAD POINTER TO ARGUMENT
	DCA TMP1	/ REMEMBER IT
	TAD I TMP1	/ LOAD ARGUMENT
	TAD NEGNL	/ LOAD -NL
	SZA CLA		/ WANT TO PRINT NL?
	 JMP NOTNL	/ IF YES, PRINT CR FIRST
	TSF		/ WAIT FOR TELEPRINTER READY
	 JMP .-1
	TAD CR		/ LOAD CR
	TLS		/ PRINT CR
	CLA
NOTNL,	TSF		/ WAIT FOR TELEPRINTER READY
	 JMP .-1
	TAD I TMP1	/ LOAD CHARACTER TO PRINT
	TLS		/ PRINT CHARACTER
	ISZ PUTCHA	/ SKIP OVER ARGUMENT
	JMP I PUTCHA	/ RETURN

SENSE,	0		/ SENSE SWITCHES
	CLA OSR
	JMP I SENSE

	PAGE		/ ARITHMETIC SUPPORT CODE

XMUL,	0		/ MULTIPLY NUMBERS (OPERATOR *)
	CLA
	TAD I XMUL	/ POINTER TO FIRST FACTOR
	DCA TMP1
	TAD I TMP1	/ FIRST FACTOR
	DCA TMP1
	ISZ XMUL	/ GO TO SECOND FACTOR
	TAD I XMUL	/ POINTER TO SECOND FACTOR
	DCA TMP2
	TAD I TMP2	/ SECOND FACTOR
	DCA TMP2
	DCA TMP3	/ CLEAR RESULT
BEGMUL,	TAD TMP1	/ LOAD FACTOR
	CLL RAR		/ GET LSB
	SNA		/ DONE YET?
	 JMP RETMUL
	DCA TMP1	/ DEPOSIT SCALED FACTOR
	SZL		/ NEED TO ADD?
	 TAD TMP2
	TAD TMP3	/ ADD SCALED FACTOR TO PRODUCT
	DCA TMP3	/ UPDATE PRODUCT
	TAD TMP2	/ LOAD FACTOR
	CLL RAL		/ SCALE FACTOR
	DCA TMP2	/ DEPOSIT SCALED FACTOR
	JMP BEGMUL	/ CONTINUE
RETMUL,	TAD TMP3	/ LOAD RESULT
	ISZ XMUL	/ SKIP OVER SECOND ARG
	JMP I XMUL	/ RETURN

XDIV,
XMOD,	0
	HLT		/ NOT IMPLEMENTED
